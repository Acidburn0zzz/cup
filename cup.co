@print = (msg, tag) ->
  return if msg is silence
  if print.last is print.last = msg
    ctrl.setAttribute \data-x, -~ctrl.getAttribute \data-x
  else
    ctrl.setAttribute \data-x, 1
    lmn = document.createElement tag || \span
    lmn.appendChild document.createTextNode msg + \\n
    pout.insertBefore lmn, pout.firstChild
  silence
print.last = silence = Date()

@clear = -> pout.innerHTML = ''
@say = self.puts = (...xs) -> print xs.join \\n
@warn = (er) -> print "#{er}", \em
@p = -> say (JSON.stringify x, null, 1 for x of arguments).join \\n

$ = (id) -> document.getElementById id
code = $ \code
ctrl = $ \ctrl
pout = $ \pout
btns = {}
poem = '''
"Coco #{Coco.VERSION}"




###
Not to abandon the evil parts
But to embrace the good parts
'''
kick = ->
  code.focus()
  {value} = code
  location.hash = @id.charAt() + \: + encode value if value isnt poem
  try r = Coco[@id] value, {+bare}
  catch e then warn e; throw e
  switch @accessKey
  case \t then r = (tag for [tag] of r).join ' '
  case \n then r.=lines.join('').slice 1
  puts r
encode = -> encodeURI(it).replace /[()]/g, -> escape it

for Key of <[ Tokens Nodes Compile Eval ]>
  btn = document.createElement \button
  btn.id = key = Key.toLowerCase()
  btn.onclick = kick
  btn.innerHTML = Key
  btns[key] = btns[btn.accessKey = key.charAt 0] = ctrl.appendChild btn

for k, b in {C: eva1 = btns.eval, S: cmpl = btns.compile}
  b.innerHTML += " <small><kbd>\\#{k}-RET"
document.onkeydown = (ev) ->
  return if (ev ||= event).keyCode isnt 13 || ev.altKey || ev.metaKey
  return unless b = ev.ctrlKey && eva1 || ev.shiftKey && cmpl
  b.click()
  false
setTimeout ->
  code.value = if cf = location.hash.slice 1
    try cf = decodeURIComponent cf
    {$1: op, rightContext: cf} = RegExp if /^([a-v]+):/.test cf
    cf
  else
    poem
  (if op then btns[op.toLowerCase()] else eva1).click()

@onfocus = -> code.focus()
